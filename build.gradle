buildscript {
    ext {
        springBootVersion = '1.4.0.M2'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


plugins {
    id "com.github.kt3k.coveralls" version "2.6.3"
}


apply plugin: "java"
apply plugin: "war"
apply plugin: "spring-boot"
apply plugin: "jacoco"

version = '0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all"
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}


dependencies {
    compile "org.apache.httpcomponents:httpclient:4.5.2"
    compile 'com.github.jknack:handlebars:4.0.5'
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-devtools"
    compile "org.springframework.security:spring-security-openid"
    compile "net.rakugakibox.springbootext:spring-boot-ext-logback-access:1.1" // shows access log on console (only for embedded containers)
    compile "org.postgresql:postgresql:9.4.1208"

    runtime "com.h2database:h2:1.4.191"
    providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"

    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "org.springframework.security:spring-security-test"
    testCompile "org.jsoup:jsoup:1.9.1"
}


jacoco {
    toolVersion = "0.7.6.201602180812"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}


task copyToolsToLibs(type: Copy) {
    from "tools"
    into "build/libs"
}

task stage {
    dependsOn assemble, copyToolsToLibs
}

tasks.stage.doLast() {
    delete "build/libs/a-cat-named-nyan-${version}.war.original"
}
